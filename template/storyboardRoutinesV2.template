; V2
;***************************************************************************
; in U pointer to lane RAM
; in X pointer to lane Data
initLane:
 stx LANE_ROM_DATA, u
 ldx ,x ; first element
  clr LANE_CURRENT_ELEMENT,u
 

init_element:
 lda ELEMENT_BEHAVIOUR,x
 anda #BEHAVIOUR_ANIMATION_LOOP
 sta LANE_ANIM_LOOP,u

 lda ELEMENT_MOVE_SCALE,x ;  
 sta LANE_CURRENT_MOVE_SCALE,u

 lda ELEMENT_ANIM_DELAY ,x 
 sta LANE_DELAY_COUNT, u
 clr LANE_ANIM_COUNT, u

; SCALE
 lda ELEMENT_SCALE_FROM,x ;  
 sta LANE_CURRENT_DRAW_SCALE,u
 lda ELEMENT_SCALE_DELAY,x 
 sta LANE_SCALE_DELAY,u
 lda ELEMENT_SCALE_INCREASE,x
 sta LANE_SCALE_INCREASE,u
 lda ELEMENT_SCALE_TO,x ;  
 sta LANE_SCALE_TO,u


; INTENSITY
 lda ELEMENT_INTENSITY_FROM,x ;  
 sta LANE_CURRENT_INTENSITY,u
 lda ELEMENT_INTENSITY_DELAY,x 
 sta LANE_INTENSITY_DELAY,u
 lda ELEMENT_INTENSITY_INCREASE,x
 sta LANE_INTENSITY_INCREASE,u
 lda ELEMENT_INTENSITY_TO,x ;  
 sta LANE_INTENSITY_TO,u

; POS
 ldd ELEMENT_POS_FROM,x ;  
 std LANE_CURRENT_POSITION,u
 ldd ELEMENT_POS_DELAY,x 
 std LANE_POS_DELAY,u
 ldd ELEMENT_POS_INCREASE,x
 std LANE_POS_INCREASE,u
 ldd ELEMENT_POS_TO,x ;  
 std LANE_POS_TO,u

 ldd ELEMENT_DRAW,x
 std LANE_DRAW,u
 
 ldd ELEMENT_END_COUNT, x
 std LANE_ELEMENT_COUNT, u
draw_pause: ; some direct RTS
 rts

; pointer to lane RAM in Y
initNextElement

 ldx LANE_ROM_DATA,y
 lda LANE_CURRENT_ELEMENT, y
 asla
 ldx a,x

not_gotoElement:
 tfr y,u
 inc LANE_CURRENT_ELEMENT,y
 lda LANE_CURRENT_ELEMENT,y

 ldx LANE_ROM_DATA,y
 asla
 ldx a,x
 bne not_last_element
 lda #$ff
 sta LANE_CURRENT_ELEMENT,y
 bra not_gotoElement
not_last_element
 jsr init_element
;***************************************************************************

; pointer to lane RAM in Y
doLane
 ldx LANE_ELEMENT_COUNT,y
 leax -1,x
 stx LANE_ELEMENT_COUNT,y ; dec element counter

 beq initNextElement
continueWithElement

 ldx LANE_ROM_DATA,y
 lda LANE_CURRENT_ELEMENT, y
 asla
 ldx a,x

; in x pointer to ROM element data struct
 dec LANE_DELAY_COUNT,y
 bne drawCurrentAnimationFrame
 lda ELEMENT_ANIM_DELAY ,x 
 sta LANE_DELAY_COUNT, y
 inc LANE_ANIM_COUNT, y

 CLRA
    LDB      LANE_ANIM_COUNT,y     ; current animation frame
    ASLB                          ; times two since it is a word pointer
 ROLA
    LDU      ,x               ; address of data 
    LDU      d,U
 bne drawCurrentAnimationFrame
; otherwise this animation is done
; check for loop - or init next lane element!
 tst LANE_ANIM_LOOP,y
 bne loopAnim
 
 ; stay at current frame
 dec LANE_ANIM_COUNT, y


 bra drawCurrentAnimationFrame
loopAnim:
 clr LANE_ANIM_COUNT, y ; for now reset animation
; todo check for "stay"continueWithElement

drawCurrentAnimationFrame:
 lda LANE_SCALE_INCREASE,y
 beq noScaleChange
 dec LANE_SCALE_DELAY,y
 bne noScaleChange
 adda LANE_CURRENT_DRAW_SCALE,y
 sta LANE_CURRENT_DRAW_SCALE,y
 suba LANE_SCALE_TO,y
 bne scale_final_not_reached 
 clr LANE_SCALE_INCREASE,y ; zero means no change
 bra noScaleChange
scale_final_not_reached
 lda ELEMENT_SCALE_DELAY,x
 sta LANE_SCALE_DELAY,y
noScaleChange

 lda LANE_INTENSITY_INCREASE,y
 beq noIntensityChange
 dec LANE_INTENSITY_DELAY,y
 bne noIntensityChange
 adda LANE_CURRENT_INTENSITY,y
 sta LANE_CURRENT_INTENSITY,y
 suba LANE_INTENSITY_TO,y
 bne intensity_final_not_reached
 clr LANE_INTENSITY_INCREASE,y ; zero means no change
 bra noIntensityChange
intensity_final_not_reached 
 lda ELEMENT_INTENSITY_DELAY,x
 sta LANE_INTENSITY_DELAY,y
noIntensityChange

 lda LANE_POS_INCREASE_X,y
 beq noXChange
 dec LANE_POS_DELAY_X,y
 bne noXChange
 adda LANE_CURRENT_POSITION_X,y
 sta LANE_CURRENT_POSITION_X,y
 suba LANE_POS_X_TO,y
 bne X_final_not_reached
 clr LANE_POS_INCREASE_X,y ; zero means no change
 bra noXChange
X_final_not_reached
 lda  ELEMENT_POS_DELAY_X,x
 sta LANE_POS_DELAY_X,y
noXChange

 lda LANE_POS_INCREASE_Y,y
 beq noYChange
 dec LANE_POS_DELAY_Y,y
 bne noYChange
 adda LANE_CURRENT_POSITION_Y,y
 sta LANE_CURRENT_POSITION_Y,y
 suba LANE_POS_Y_TO,y
 bne Y_final_not_reached
 clr LANE_POS_INCREASE_Y,y ; zero means no change
 bra noYChange
Y_final_not_reached
 lda ELEMENT_POS_DELAY_Y,x
 sta LANE_POS_DELAY_Y,y
noYChange

 lda LANE_CURRENT_INTENSITY,y
 bmi noIntensity
 _INTENSITY_A
noIntensity
; todo set intensity - when another vlist that extended sync is done

 CLRA
    LDB      LANE_ANIM_COUNT,y     ; current animation frame
    ASLB                          ; times two since it is a word pointer
 ROLA
    LDU      ,x               ; address of data 
    LDU      d,U

    LDd      LANE_CURRENT_POSITION,y     ; current animation frame
    tfr      d,x                          ; in x position of list 

; A scale positioning 
; N scale move in list 
 ldd LANE_CURRENT_MOVE_SCALE, y
    JSR      [LANE_DRAW, y]      ; Vectrex BIOS print routine 

 rts
