:title Drawing vectors - differently

Simply put vector drawing goes like this:
- place the beam on the screen
- switch the light on
- move the beam with light switched on
- switch the light off

Less simple:~~
The way BIOS (and most other implementations) draw vectors is:~~
(drawing characters or raster data is slightly different)
- place the beam somewhere (MoveTo)
- set the scale factor (Timer1 Lo)
- set y strength (integrator Y)
- set x strength (integrator X)
- write $ff to shift register (the light is switched on)
- start timer 1 (which, starts the ~RAMP (vector beam is moving\))
- wait for timer 1 to expire (~RAMP is switched off)
- write $00 to shift register (switch the light off)

A short via side explanation (for more in depth see "Vectrex internals -> Analog").

**~RAMP~~**
Moving the beam internally is realized using integrator circuits which are connected to the deflection coils. Whether the integrators are integrating or not is controlled by a single "flag" (one BIT), which is called ~RAMP. 

~RAMP is a zero active flag, meaning the integrators are WORKING when the ~RAMP flag equals zero, this is indicated by the "~" in front of the flag.

**~BLANK~~**
Switching the light on and off is internally nearly the same thing, the brightness of the light is set equally to the strength of the vectors [NOT using the blank flag], and the circuit telling the CRT to actually send a "strong" electron beam (which illuminates the phosphor coated tube) is not really that much different than the deflection coil circuits.
~~What the "~RAMP" signal is for moving - that is the "~BLANK" signal for light on off.~~
The ~BLANK is also a single (zero active) flag, which tells vectrex to switch the light off ~BLANK equals zero, or to switch the light on (~BLANK non zero).

**VIA~~**
To communicate the state of the flags (set/unset) VIA 6522 is used.

~RAMP flag is connected the seventh bit of VIA port B (in the following the short form of PB7 is used (**P**ort **B** bit **7**).

~BLANK flag is connected to the VIA Control line Port B 2 (in the following the short form CB2 is used (**C**ontroll line Port **B 2**\))

**Versatile~~**
As the name VIA (Versatile Interface Adapter) implies - there are more than one way to handle things. Via has a couple of control registers, that influence the way it behaves.
In relation to the above two "flags" (PB7 = ~RAMP and CB2 = ~BLANK) following information is "interesting":~~

Default BIOS settings:~~
VIA register $b (auxiliary control register) is set to $98, which in bits is %10 0 110 00.
(See also Vide documentaion: Vectrex internals-> VIA)~~
- Bit 7 and 6 ["10"]: sets the behaviour of Timer T1, in short "10" means:~~//count down to zero and than stop, also while counting down set PB7 to zero, when countdown is finished, set PB7 to one//
- Bit 4, 3 and 2 ["110"]: sets the bevaviour of the SHIFT Register of VIA, the "combined" behaviour setting result is:~~//every two processor cycle shift out the MSB (most significant bit) to CB2, at the same time shift the MSB back to the LSB (least significant bit). Keep on shifting till I tell you something else [the actual shifting starts once the Shiftreg is read or written to]//.

What this boils down to is the above described "less simple" way of drawing a vector.~~

Starting the timer 1 "automatically" starts moving the vector. Programatically writing "$ff" to the shift register switches the light on for every shift cycle following (~BLANK = 1 means the light is on).

When the timer t1 reaches zero, the vector automatically stops moving. Than one programatically has to switch the light of by writing 0 to the shift register.~~
(Note: it is better to do a ::~~__clra~~__sta via\_shiftreg::~~than to do a~~::__clr via\_shiftreg::, ~~the speed (processor cycles) is the same, but doing a "::clr via\_shiftreg::"  also READS the shiftreg, which inherently starts shifting and does output at least one other shift cycle to CB2). ~~
~~
As you might notice, the switch light off using the above method always switches the light off after the moving stops - actually the light is switched off (due to instructions being not immediate) after some additional cycles (at least 4). Meaning the light is kept on for that number of cycles for to long!

You can see that on your vectrex, each vector has a brighter spot at its end, this is because the light is switched off a few cycles to late.

:h2 Differently

As you might have guessed with the above mentioning of "versatile" - you can also access the above flags differently.

One other ways to access both bits is to set them "manually".

Below given example (in X register given a Draw\_VLc compatible vectorlist) draws vectors "manually" - meaning without using a timer and without using the shift register.

Following settings were used:~~
VIA register $b (auxiliary control register) is set to $00, which in bits is %00 0 000 00.
(See also Vide documentaion: Vectrex internals-> VIA)~~
- Bit 7 and 6 ["00"]: sets the behaviour of Timer T1, in short "00" means:~~//Do not influence PB7 by timer 1.//
- Bit 4, 3 and 2 ["000"]: sets the behaviour of the SHIFT Register of VIA, the "combined" behaviour setting result is:~~//Disable Shift register activity//.

By setting the via auxiliary control register to above values, both flags can be set manually. 
- PB7 ~~can manually be set by writing appropriate values to  "VIA\_port\_b" (Bit 7).
- CB2~~ can manually be set by setting appropriate values to "VIA\_cntl" (Bit 5, 6 and 7 are responsible for CB2 behaviour)

:div code
::
fixDraw: ~~
; setup fixdraw~~
________lda___,x+____________; load the count of vectors to draw ~~
________sta___tmp____________; and remember them somewhere ~~
________LDA___#$81____________~~
________sta___<VIA\_port\_b____; mux disabled, ramp disables ~~
________LDA___#$00____________~~
________STA___<VIA\_aux\_cntl__; Shift reg mode = 000 free disable, T1 PB7 disabled~~ 
noEnd:~~
________lda___#$ce ~~
________STA___<VIA\_cntl______; ZERO disabled, and BLANK enabled ~~
nextv:~~
________ldd___,x++___________; a = y, b = x ~~
________STA___<VIA\_port\_a____; Y -> Send it to the D/A ~~
________lda___#$80 ~~
________sta___<VIA\_port\_b____; ramp disabled, mux enabled -> to Y ~~
________inca__~~
________sta___<VIA\_port\_b____; ramp disabled, mux disabled -> to Y ~~
________STB___<VIA\_port\_a____; X -> Send it to the D/A ~~
________ldd___#$8101 ~~
________stb___<VIA\_port\_b____; below are x cycles RAMP switched on!~~
________ldb___#$ee ~~
________stb___<VIA\_cntl______; ZERO disabled, and BLANK disabled ~~
~~
________nop___10 ; DELAY~~
~~
________sta___<VIA\_port\_b____; disable ramp ; should again be $81 ~~
; and x +y cycles light switched on~~
________nop___2 ~~
________dec___tmp ~~
________bpl___noEnd ~~
________ldb___#$ce ~~
________stb___<VIA\_cntl______; ZERO disabled, and BLANK enabled ~~
________LDA___#$98 ~~
________STA___<VIA\_aux\_cntl ~~
________rts___~~
::~~
:/div

The line with "::nop___10_; DELAY::" is responsible for the size of the resulting image (much like the scale factor).

~~**Note:**~~
Although the code above suggests otherwise  in consideration of VIA internal timings - the above vector drawing done is (at least using my vectri) cycle exact. ~RAMP and ~BLANK are set and unset at the correct times, and there are no bright spots on either side of the drawn vectors. (~BLANK is switched off for 20 cycles //longer// than ~RAMP - I have no clue what VIA is doing in those 20 cycles!)~~

~~**Note:**~~
The timings given in above example have been experimentally gotten by trial an error using real life vectrex systems. While Vide is able to display the result correctly, emulation of VIA internal timing is not good enough to trust the emulator on timing experiments.

